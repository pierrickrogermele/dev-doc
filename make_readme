#!/usr/bin/env perl

use strict;
use warnings;
use Data::Dumper;
use File::Basename;

sub get_folders($) {
	my ($path) = @_;

	my @dirs = ();

	for my $f (<$path/*>) {
		push(@dirs, $f) if -d $f;
	}

	return sort @dirs;
}

sub get_md_file_title($) {
	my ($file) = @_;

	my $title;

	open my $fd, "<$file";
	chomp($title = <$fd>);
	$title =~ s/^# *//;
	close $fd;

	return $title;
}

sub get_md_files($) {
	my ($path) = @_;

	my @files;

	for my $f (<$path/*.md>) {
		next if $f =~ /\/README.md$/;
		my $t = get_md_file_title($f);
		push(@files, {title => $t, path => $f});
	}

	return \@files;
}

sub list_markdown_files {

	my @dirs = ({'path' => '.', title => 'Root'});

	# List subfolders
	for my $d (get_folders($dirs[0]->{'path'})) {
		my $t = ucfirst(basename($d));
		push(@dirs, {title => $t, path => $d});
	}

	# List markdown files
	for my $dir (@dirs) {
		my $md_files = get_md_files($dir->{path});
		$dir->{'md_files'} = $md_files;
	}

	return \@dirs;
}

sub list_cols_to_print($) {

	my ($mdfiles) = @_;

	my @cols = ();

	# Filter out directories with no md files
	for my $i (0 .. $#$mdfiles) {
		push(@cols, $i) if 0 != @{$$mdfiles[$i]->{md_files}};
	}

	return @cols;
}

sub get_size_of_biggest_col($) {

	my ($mdfiles) = @_;

	my $s = 0;

	for my $i (0 .. $#$mdfiles) {
		my $n = @{$$mdfiles[$i]->{md_files}};
		$s = $n if $n > $s;
	}

	return $s;
}

sub print_table($) {

	my ($mdfiles) = @_;

	my @cols = list_cols_to_print($mdfiles);
	my $max_length = get_size_of_biggest_col($mdfiles);

	# Write table header
	for my $i (@cols) {
		print " | " if $i != $cols[0];
		print($$mdfiles[$i]->{title});
	}
	print "\n";
	for my $i (@cols) {
		print " | " if $i != $cols[0];
		print("----------");
	}
	print "\n";

	# Write links to Markdown files
	for my $line (0 .. ($max_length - 1)) {
		for my $i (@cols) {
			print " | " if $i != $cols[0];
			my $files = $$mdfiles[$i]->{md_files};
			if ($line < @$files) {
				print '['.$$files[$line]->{title}.']('.$$files[$line]->{path}.')';
			}
		}
		print "\n";
	}
}

sub main() {

	my $mdfiles = list_markdown_files();

	print "# Index of Pierrick's public notes\n";
	print "\n";

	print_table($mdfiles);
}

main()
